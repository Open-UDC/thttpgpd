dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/thttpd.c)

AC_CANONICAL_SYSTEM

AC_PROG_CC
AC_LANG([C])

AC_SYS_LARGEFILE

V_CCOPT="-O"
if test "$GCC" = yes ; then
	AC_MSG_CHECKING(gcc version)
	AC_CACHE_VAL(ac_cv_lbl_gcc_vers,
	    ac_cv_lbl_gcc_vers=`$CC -dumpversion 2>&1 | \
		sed -e 's/\..*//'`)
	AC_MSG_RESULT($ac_cv_lbl_gcc_vers)
	if test "$ac_cv_lbl_gcc_vers" -gt 1 ; then
		V_CCOPT="-O2"
	fi
fi
if test -f .devel ; then
	V_CCOPT="-g $V_CCOPT -Wall -Wmissing-prototypes -Wstrict-prototypes"
fi

dnl
dnl maybe this should be a loop
dnl
AC_MSG_CHECKING(how to link static binaries)
AC_CACHE_VAL(ac_cv_lbl_static_flag,
    ac_cv_lbl_static_flag=unknown
    echo 'main() {}' > conftest.c
    if test "$GCC" != yes ; then
	    trial_flag="-Bstatic"
	    test=`$CC $trial_flag -o conftest conftest.c 2>&1`
	    if test -z "$test" ; then
		    ac_cv_lbl_static_flag="$trial_flag"
	    fi
	    rm -f conftest
    fi
    if test "$ac_cv_lbl_static_flag" = unknown ; then
	    trial_flag="-static"
	    test=`$CC $trial_flag -o conftest conftest.c 2>&1`
	    if test -z "$test" ; then
		    ac_cv_lbl_static_flag="$trial_flag"
	    fi
	    rm -f conftest
    fi
    rm conftest.c)
AC_MSG_RESULT($ac_cv_lbl_static_flag)
if test "$ac_cv_lbl_static_flag" != unknown ; then
	V_STATICFLAG="$ac_cv_lbl_static_flag"
fi

AC_MSG_CHECKING(for __progname)
AC_CACHE_VAL(ac_cv_extern__progname,
    AC_TRY_LINK([],
	[extern char *__progname; 
	puts(__progname)],
	ac_cv_extern__progname=yes,
	ac_cv_extern__progname=no))
if test $ac_cv_extern__progname = yes ; then
	AC_DEFINE(HAVE__PROGNAME)
	AC_MSG_RESULT(yes)
else
	AC_MSG_RESULT(no)   
fi

AC_CHECK_HEADERS(grp.h memory.h dirent.h)
AC_CHECK_HEADERS(poll.h sys/poll.h sys/devpoll.h,break,AC_MSG_ERROR("Missing at least a *poll.h header"))
AC_CHECK_HEADERS(syslog.h sys/syslog.h,break,AC_MSG_ERROR("Missing a required header file"))
AC_CHECK_HEADERS(fcntl.h sys/stat.h gpgme.h semaphore.h,,AC_MSG_ERROR("Missing a required header file"))
AC_HEADER_TIME
AC_HEADER_DIRENT

d="/usr/local/v6/lib"
AC_MSG_CHECKING(for $d)
if test -d $d; then
	AC_MSG_RESULT(yes (Adding -L$d to LDFLAGS))
	LDFLAGS="$LDFLAGS -L$d"
else
	AC_MSG_RESULT(no)
fi

dnl
dnl Most operating systems have gethostbyname() in the default searched
dnl libraries (i.e. libc):
dnl
V_NETLIBS=""
AC_CHECK_FUNC(gethostbyname)
AC_CHECK_FUNC(socket)

AC_CHECK_LIB(inet6, main)
AC_CHECK_LIB(gpgme-pthread, gpgme_check_version,,AC_MSG_ERROR("libgpgme-pthread.so missing (or incorrect)."))
AC_CHECK_LIB(pthread, sem_open,,AC_MSG_ERROR("libpthread.so missing (or incorrect)."))

AC_CHECK_FUNC(crypt, , AC_CHECK_LIB(crypt, crypt))
AC_CHECK_FUNC(hstrerror, ,
    AC_CHECK_LIB(resolv, hstrerror, V_NETLIBS="-lresolv $V_NETLIBS"))

AC_REPLACE_FUNCS(strerror)
AC_CHECK_FUNCS(waitpid vsnprintf setsid getaddrinfo getnameinfo gai_strerror kqueue sigset atoll)
AC_FUNC_MMAP

case "$target_os" in
solaris*)
	dnl Solaris's select() is a bad wrapper routine.
	AC_CHECK_FUNCS(poll)
	;;
*)
	AC_CHECK_FUNCS(select poll)
	;;
esac

AC_ACME_TM_GMTOFF
AC_ACME_INT64T
AC_ACME_SOCKLENT

AC_PROG_MAKE_SET
AC_PROG_INSTALL

AC_SUBST(DEFS)
AC_SUBST(V_CCOPT)
AC_SUBST(V_STATICFLAG)
AC_SUBST(V_NETLIBS)

AC_OUTPUT(Makefile src/Makefile src/cgi-src/Makefile)

